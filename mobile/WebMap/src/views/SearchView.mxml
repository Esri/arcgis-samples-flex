<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:s="library://ns.adobe.com/flex/spark"
        xmlns:esri="http://www.esri.com/2008/ags"
        initialize="initializeHandler(event)"
        viewActivate="viewActivateHandler(event)">

    <fx:Script>
        <![CDATA[
            import com.esri.ags.events.PortalEvent;
            import com.esri.ags.events.WebMapEvent;
            import com.esri.ags.portal.Portal;
            import com.esri.ags.portal.supportClasses.PortalQueryParameters;

            import mx.collections.ArrayList;
            import mx.events.FlexEvent;

            import spark.events.IndexChangeEvent;
            import spark.events.ViewNavigatorEvent;
            import spark.managers.PersistenceManager;

            private var persistenceManager:PersistenceManager = new PersistenceManager();
            private var searchText:String;
            private var searchResults:ArrayList;
            private var webMapTitle:String;
            private var currentItemId:String;

            private function initializeHandler(event:FlexEvent):void
            {
                searchText = persistenceManager.getProperty("searchText") as String;
                if (searchText)
                {
                    searchTextInput.text = searchText;
                }
                searchResults = persistenceManager.getProperty("searchResults") as ArrayList;
                if (searchResults)
                {
                    searchResultList.dataProvider = searchResults;
                }
            }

            private function viewActivateHandler(event:ViewNavigatorEvent):void
            {
                if (!searchResults || searchResults.length == 0)
                {
                    searchTextInput.setFocus();
                }
            }

            private function webmapSearch(searchString:String):void
            {
                persistenceManager.setProperty("searchText", searchTextInput.text);
                noWebMapsLabel.visible = false;
                bi.visible = searchLabel.visible = true;

                var portal:Portal = data as Portal;
                portal.addEventListener(PortalEvent.QUERY_ITEMS_COMPLETE, search_resultHandler);
                portal.queryItems(PortalQueryParameters.forQuery(searchString + ' +type:"Web Map" -type:"Web Mapping Application" -tags:basemap').withLimit(24));
            }

            private function search_resultHandler(event:PortalEvent):void
            {
                var portal:Portal = data as Portal;
                portal.removeEventListener(PortalEvent.QUERY_ITEMS_COMPLETE, search_resultHandler);

                bi.visible = searchLabel.visible = false;

                const source:ArrayList = new ArrayList();
                source.source = event.queryResult.results;
                if (source.length > 0)
                {
                    persistenceManager.setProperty("searchResults", source);
                    searchResultList.dataProvider = source;
                }
                else
                {
                    noWebMapsLabel.visible = true;
                }
            }

            private function searchResultList_changeHandler(event:IndexChangeEvent):void
            {
                bi.visible = true;
                currentItemId = event.target.selectedItem.id;
                webMapTitle = event.target.selectedItem.title;
                webMapUtil.createMapById(currentItemId);
            }

            private function webMapUtil_createMapByIdCompleteHandler(event:WebMapEvent):void
            {
                bi.visible = searchLabel.visible = false;
                if (event.item.id === currentItemId)
                {
                    navigator.pushView(MapView, { map: event.map, view: this, title: webMapTitle });
                }
            }

            private function backButton_clickHandler(event:MouseEvent):void
            {
                persistenceManager.setProperty("searchText", null);
                persistenceManager.setProperty("searchResults", null);
                navigator.popView();
            }
        ]]>
    </fx:Script>

    <fx:Declarations>
        <esri:GeometryServiceSingleton url="http://sampleserver3.arcgisonline.com/ArcGIS/rest/services/Geometry/GeometryServer"/>
        <esri:WebMapUtil id="webMapUtil" createMapByIdComplete="webMapUtil_createMapByIdCompleteHandler(event)"/>
    </fx:Declarations>

    <s:navigationContent>
        <s:Button id="backButton"
                  click="backButton_clickHandler(event)"
                  icon="@Embed('assets/back.png')"/>
    </s:navigationContent>

    <s:titleContent>
        <s:TextInput id="searchTextInput"
                     width="100%"
                     enter="webmapSearch(searchTextInput.text)"
                     prompt="Search web maps"/>
    </s:titleContent>

    <s:actionContent>
        <s:Button click="webmapSearch(searchTextInput.text)" label="Search"/>
    </s:actionContent>

    <s:List id="searchResultList"
            width="100%" height="100%"
            change="searchResultList_changeHandler(event)">
        <s:itemRenderer>
            <fx:Component>
                <s:IconItemRenderer iconField="thumbnailURL"
                                    iconHeight="54"
                                    iconScaleMode="letterbox"
                                    iconWidth="80"
                                    labelField="title"/>
            </fx:Component>
        </s:itemRenderer>
    </s:List>

    <s:Label id="noWebMapsLabel"
             top="10"
             fontStyle="italic"
             text="Sorry, no web maps found"
             visible="false"/>
    <s:Label id="searchLabel"
             horizontalCenter="0"
             text="Searching..."
             verticalCenter="-40"
             visible="false"/>
    <s:BusyIndicator id="bi"
                     horizontalCenter="0"
                     verticalCenter="+40"
                     visible="false"/>
</s:View>
